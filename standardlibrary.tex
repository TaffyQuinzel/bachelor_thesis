\chapter{Standard library}\label{chap:standardlibrary}
We will now look at the standard library and explain the evolution it went through during the project.

When we have an idea of how it should work, the choices made during the development will be clearer.

\section{Prelude}\label{sec:standardprelude}
\emph{Prelude} is the first item in the standard library and contains a few definitions making basic programming easier.
\emph{Prelude} contains the definitions which are not large enough to grant there own item within the standard library.

The .NET \emph{System} namespace is imported and the type \verb\Unit\ is created.

\begin{lstlisting}
import System

Data "unit" -> Unit
\end{lstlisting}

The \verb\Unit\ is used as an empty or null value.

Next we have the declaration and definition of a generic tuple and union.
Because they are generic \verb\TypeAlias\ is needed for the type manipulation.

\begin{lstlisting}
TypeAlias Type => "*" => Type => Type
Data 'a -> "," -> 'b -> 'a * 'b    #> 5

TypeAlias Type => "|" => Type => Type
Data "Left" -> 'a -> 'a | 'b       #> 5
Data "Right" -> 'b -> 'a | 'b      #> 5
\end{lstlisting}

A standard if-else construct is then implemented as follows:

\begin{lstlisting}
TypeAlias "Then" => Type
Data "then" -> Then
TypeAlias "Else" => Type
Data "else" -> Else

Func "if" -> Boolean^System -> Then -> 'a -> Else -> 'a -> 'a
if True^builtin then f else g -> f
if False^builtin then f else g -> g
\end{lstlisting}

The \verb\then\ and \verb\else\ \verb\Data\s are syntactic sugar and could be left out.
We have left them in to enhance the clarity of the if-else construct.

Now that we have the if-else construct we can do basic boolean math.
It also makes the use of multiple rules an option and not a necessity, which in turn creates more freedom for the programmer.

Here we see how \emph{System} of .NET is used.
The boolean of .NET can be imported as shown, however the values \verb\True\ and \verb\False\ cannot be imported.
They are built in .NET itself.
This is why the boolean literals are built into MC as well and need to be called using \verb\builtin\.


\subsection{Match}
Next we have the \verb\match\ function.
\verb\match\ is used to check the constructor of the \emph{pipe} operator and makes computations with the \emph{pipe} easier.

It takes a variable, matches it on either \verb|Left| or \verb|Right| and executes the function specified.

\begin{lstlisting}
TypeAlias "With" => Type
Data "with" -> With

Func "match" -> ('a | 'b) -> With -> ('a -> 'c) -> ('b -> 'c) -> 'c
\end{lstlisting}

There is some syntactic sugar created to make it clearer how \verb\match\ works.
The first argument is the union which needs to get matched.
The arguments \verb\('a -> 'c)\ and \verb\('b -> 'c)\ are the functions to be executed on each branch of the match.

In this case the two functions both take an argument from the union, the \verb\'a\ and the \verb\'b\, and both return \verb\'c\.

Next we have the definition of \verb\match\.
\verb\match\ needs a definition for both cases of the match, namely the \verb\Right\ and the \verb\Left\.

\begin{lstlisting}
match (Left x) with f g -> f x
match (Right y) with f g -> g y
\end{lstlisting}

When it matches the \verb\Left\ it executes function \verb\f\ with \verb\x\ as its argument.
And when it matches the \verb\Right\ it executes function \verb\g\ with \verb\y\ as its argument.

% \textbf{THIS IS PROBABLY NOT POSSIBLE}
% However this does not take into account the possibility of nested pipes.
% For this we have written a third definition of \verb\match\:

% \begin{lstlisting}
% match y with g h -> res
% --------------------
% match (Right y) with f (g h) -> res
% \end{lstlisting}

% This definition is placed above the previous defined \verb\match\ which matches on \verb\Right\.
% Then there is first checked on nested pipes and when there are none the actual value is checked.

% In this manner there is no possibility of skipping any nested pipes.


\subsection{List}
We have a generic list implementation, which very useful considering MC has no built in arrays.
For this we need to create a list that can work with types.

The list is declared with a union.
The union is necessary because we need an end to the list.

\begin{lstlisting}
TypeAlias "List" => Unit | (Type * (List Type))
\end{lstlisting}
% List unit => Left unit
% List ('a * 'b) => Right ('a * 'b)

The list is created with a tuple.
An end to the list is created with a union of the list tuple and \verb\Unit\.

We also need the basic operators to create lists.
This is done with \verb\::\, which takes a type and a list of that same type.
And with \verb\empty\ an empty list is created.

\begin{lstlisting}
Data 'a -> "::" -> 'b -> List (Right ('a * 'b))
Data "empty" -> List (Left unit)
\end{lstlisting}

With \verb\::\ we can specify the head and tail of a list.

But we would also like to concat lists together.
Concatenation is done with the \verb\@\ operator.

\begin{lstlisting}
Func List 'a -> "@" -> List 'a -> List 'a  #> 200
empty @ l -> l
(x :: xs) @ l -> x :: (xs @ l)
\end{lstlisting}

And when we want to apply a function to the entire list we call \verb\map\.
\verb\map\ executes a function and creates a new list, which it then returns.

\begin{lstlisting}
Func "map" -> List 'a -> ('a -> 'b) -> List 'b
map empty f -> empty
map (x :: xs) f -> (f x) :: (map xs)
\end{lstlisting}

When we want to find one or more elements in the list we can call \verb\filter\.
It checks the entire list using a predicate function and returns the matching elements.

\begin{lstlisting}
Func "filter" -> List 'a -> ('a -> Boolean^System) -> List 'a
filter empty p -> empty

(if p x then
  (x :: (filter xs p))
  else
  (filter xs p)) -> res
-------------------------
filter (x :: xs) p -> res
\end{lstlisting}

The programmer can specify the predicate function \verb\p\.


\subsection{Evolution of prelude}
We will now go over the evolution of the separate parts of \emph{prelude}.

\subsubsection{Boolean}
Boolean currently does not exist anymore as a separate part of the standard library.
Here we will see why this is the case.

Boolean was created to implement boolean logic.
After many iterations it was discarded due to the evolution of the language.

When first implementing Boolean it was part of \emph{Prelude}.

\begin{lstlisting}
Data "True" -> Boolean^System
Data "False" -> Boolean^System
\end{lstlisting}

This might seem correct at first glance, but \verb\True\ and \verb\False\ have no meaning here.
There is no way to check whether a boolean value is true or false.
That is why a new notation had to be created for the boolean literals, which ended up into a module with the literals as \verb\Func\s.

\begin{lstlisting}
import System

TypeFunc "Boolean" => Module
Boolean => Module {
  Func "True" -> Boolean^System
  Func "False" -> Boolean^System
}
\end{lstlisting}

The Boolean module could then be implemented in \emph{prelude}.

\begin{lstlisting}
TypeFunc "boolean" => Boolean
boolean => Boolean {
   True -> True^builtin
   False -> False^builtin
}
\end{lstlisting}

The boolean literals could now be called using \verb\True^boolean\.
This way was noticeably the same as what happened inside the implementation of \verb\boolean\.
The choice was then made to call the boolean literals directly with \verb\True^builtin\, which made the boolean module obsolete.

This choice brings us to the current state of the boolean literals.


\subsubsection{Match}
\emph{Match} started as a separate part of the standard library containing the \verb\match\ module.
We will now look at how it was first created and how it has evolved into \emph{prelude}.

The first iteration of the \verb\match\ module started with the import of \emph{prelude} and the declaration of the \verb\match\ module.

\begin{lstlisting}
import prelude

TypeFunc "match" => Type => Module
\end{lstlisting}

So far there are no problems yet with us a module for \verb\match\.

Next we see the first part of implementation of the \verb\match\ module:
Some syntactic sugar is created and the actual function is defined which will do the matching.

\begin{lstlisting}
match ('a | 'b) => Module {
  Data "with" -> With

  TypeFunc "Left" => Type
  Left => 'a

  TypeFunc "Right" => Type
  Right => 'b

  Func "doMatch" -> 'c -> With -> (Left -> 'd) -> (Right -> 'd) -> 'd
\end{lstlisting}

Now we see why the module is needed.
It was thought that the \verb\match\ module would get a separate instance for every match that was executed.

When separate functions, like these, are needed to check the validity of another function it is better to group them together inside a module.

The definitions of \verb\doMatch\ have not changed compared to what they are currently.

  % doMatch y with g h -> res
  % --------------------
  % doMatch (Right y) with f (g h) -> res
\begin{lstlisting}
  doMatch (Left x) with f g -> f x
  doMatch (Right y) with f g -> g y
}
\end{lstlisting}

It became clear that \verb\Left\ and \verb\Right\ were not necessary, because they were simply passing type of the union.

With this removal the use of a module became obsolete, because we do not need to group a single function.
The \verb\doMatch\ function was then renamed \verb\match\ and placed inside \emph{prelude} directly.


% \subsubsection{Recursive match}
% The first itteration of match could not match nested pipes.
% It could only detect a direct match.
% As it only had the direct matches without the function that checks if \verb\Right\ is nested.

% The solution could have been left to the programmer.
% He would have to create a separate match statement for every level of nesting.
% This would be quite inconvenient for the programmer.

% For that reason the recursive functionality was added.

\subsubsection{List}
The first list was put together with the list monad.
This seemed the logical choice at the time, because the list monad needed a list implementation to use inside the monad.

Monads will be explained in section~\ref{sec:standardmonad} and the list monad in section~\ref{sec:standardmonadlist}.

When coming back to the list monad it became apparent that \verb\List\ was not specific to the list monad.
It could be used without the monad as a regular list implementation.

The choice was then made to move it to \emph{prelude}.


\section{Number}
\emph{Number} is created to give the user a generic interface to create numbers.

Because of the import system of MC, MC can directly import the integer and float types with all their functions from .NET.
\emph{Number} is therefor used for self defined number types.

\emph{Number} is build up from different modules to give the programmer the freedom to choose what their custom numbers can do.
The modules are arranged according to the mathematical groupings to built up numerical operators~\cite{bourbaki1998commutative}.

It starts with the \verb\MonoidAdd\ module.
It declares the \verb\+\ operator for the custom number.

\begin{lstlisting}
TypeFunc "MonoidAdd" => Type => Module
MonoidAdd 'a => Module {
  Func 'a -> "+" -> 'a -> 'a #> 60
  Func "identityAdd" -> 'a
}
\end{lstlisting}

\verb\MonoidAdd\ needs the identity as a base number from which the operations will work.

Next we have the \verb\GroupAdd\ module.
\verb\GroupAdd\ inherits everything from the module \verb\MonoidAdd\ and adds the \verb\-\ operator.

\begin{lstlisting}
TypeFunc "GroupAdd" => Type => Module
GroupAdd 'a => Module {
  inherit MonoidAdd 'a
  Func 'a -> "-" -> 'a -> 'a #> 60
}
\end{lstlisting}

We do the same for multiplication and dividing.

\begin{lstlisting}
TypeFunc "MonoidMul" => Type => Module
MonoidMul 'a => Module {
  Func 'a -> "*" -> 'a -> 'a #> 70
  Func "identityMul" -> 'a
}

TypeFunc "GroupMul" => Type => Module
GroupMul 'a => Module {
  inherit MonoidMul 'a
  Func 'a -> "/" -> 'a -> 'a #> 70
}
\end{lstlisting}

Now we have declared the basic number operations of addition, subtraction, multiplication and dividing.

We can combine them into a basic number like so:

\begin{lstlisting}
TypeFunc "Number" => Type => Module
Number 'a => Module {
  inherit GroupAdd 'a
  inherit GroupMul 'a
}
\end{lstlisting}

Or create a Vector without the dividing operator, because vectors cannot be divided~\cite{hazewinkel2013encyclopaedia}.

\begin{lstlisting}
TypeFunc "Vector" => Type => Module
Vector 'a => Module {
  inherit GroupAdd 'a
  inherit MonoidMul 'a
}
\end{lstlisting}

Of course all the operators still have to be defined, but that is up to the programmer.
This gives the programmer the freedom to choose what the operators do.


\subsection{Evolution of number}
During development \emph{number} was used to create integers and floats.
This was before the .NET import system was in place.
Currently it is only needed for user defined number types.

The first iteration of \emph{number} was just one module with all the operators declared.
The first iteration works for the standard number types like \verb\Integer\ and \verb\Float\, but not \verb\Vectors\, because they cannot be divided.

For this reason the current \emph{number} design is similar to the mathematical way.
When implementing \emph{number} we only define what we need, which reduces the overhead generated.


\section{Record}\label{sec:standardrecord}
With \emph{Record} we can create a list of key/value pairs which can be searched and manipulated.
The special thing about this record is that it does all the searching on compile time.
This makes the runtime code much faster.

% When using records in practice we see that not everything can will be known on compile time.
% Which makes a record that works on compile time useless.

% The way MC fixes this is to inline the code on compile time.
% In this way the code that needs to be actually executed can be optimised by the compiler after which it is executed on runtime.

Record is declared as a module and contains \verb\TypeFunc\s to make it work at compile time.

\begin{lstlisting}
TypeFunc "Record" => Module
Record => Module{
  TypeFunc "Label" => String
  TypeFunc "Field" => Type
  TypeFunc "Rest" => Record
\end{lstlisting}

Here we see the declarations which are the basis of a list of key/value pairs.
\verb\Label\ acts as the key, \verb\Field\ as the value paired with the key and \verb\Rest\ contains the rest of the record.

When there is just one pair in the record we can get that record out easily, as we already have it.
But when there are multiple record entries in a record we need a function to search through the record to find the sub record we want to have.
For this the \verb\get\ function was created.

\begin{lstlisting}
  TypeFunc "get" => String => Record => Record

  (if (l == Label^rs) then
    rs
  else
    get l Rest^rs) => res
  -----------------------
  get l rs => res
\end{lstlisting}

The \verb\get\ function takes a labels and a record.
It checks the label of the record and if it is the same it returns the current record and if not the it recursively calls itself with the rest of the record.

To manipulate these records there the \verb\set\ function was created.
The \verb\set\ function takes a label, a field and a record.
The field is the new value that needs to be paired with the label.

\begin{lstlisting}
  TypeFunc "set" => String => Type => Record => Record
  (if (l == Label^rs) then
    RecordEntry l f rs
  else
    (set l f Rest^rs -> rs'
     RecordEntry Label^rs Field^rs rs')) => res
  -------------------------
  set l f rs => res
}
\end{lstlisting}

Because MC does not allow direct manipulation of values, the set function returns a new record instead of changing the value of the specified record.
When the record does not match the current record \verb\set\ is called recursively, and the result is used as the new \verb\Rest\ for the current record.

With this the declaration of the record is complete.
Now we can look at how the definition of a record works.

First we need to create a record entry.
This is done with the \verb\TypeFunc\ \verb\RecordEntry\.

\begin{lstlisting}
TypeFunc "RecordEntry" => String => * => Record => Record
RecordEntry label field rest => Record{
  Field => field
  Label => label
  Rest => rest
}
\end{lstlisting}

We also need to create an empty record entry so we can end the record.
This is done by the \verb\TypeFunc\ \verb\EmptyRecord\.

\begin{lstlisting}
TypeFunc "EmptyRecord" => RecordEntry
EmptyRecord => RecordEntry {
  Field => Unit
  Label => Unit
  Rest => Unit
}
\end{lstlisting}

The programmer has to use \verb\EmptyRecord\ as the \verb\rest\ argument of the first record entry.
This creates an end to the record.


\subsection{Updatable record}
The current record can only be instantiated and not updated.
To expand the record to be updatable we need to add the update function.

First we declare and define a \verb\TypeFunc\ which creates a new record from the original with inherit.

\begin{lstlisting}
TypeFunc "updatableRecord" => Record => Record
updatableRecord r => Module {
   inherit r
\end{lstlisting}

The new module now contains everything of record \verb\r\.

Then we declare and define the update function.

\begin{lstlisting}
  TypeFunc "update" => Record => Type => Record
  r == Empty
  --------------------
  update r dt => Empty
}
\end{lstlisting}

Only the definition of \verb\update\ which checks for an empty record can be defined.
It is the only situation in which we can be certain what the return value will be, namely \verb\Empty\.

If the record is not \verb\Empty\ we cannot know what the programmer wants to do with \verb\update\.
That is why we leave that definition of \verb\update\ up to the programmer.



\subsection{Evolution of record}
Several parts of \verb\Record\ have changed.
We will go over them here.

\subsubsection{Set}
The original version of \verb\set\ checked the field-argument to be the same type of \verb\Field\ inside the record.
For this \verb\Fields\ was needed, which contained the type of \verb\Field\.

However the checking of \verb\Fields\ needed to happen in the declaration of the \verb\set\.
This required the declaration to have premises and this is not possible, because the declaration cannot be variable.

The field-argument also did not need to be checked, because \verb\Field\ of a record can be anything.\footnote{As described in section~\ref{sec:standardrecord}.}

\subsubsection{Set return}
The return value of \verb\Set\ was first only the record that was changed and not all the previous records in the record structure.
This was fixed by returning the recursive call to \verb\set\ as the new value of the current record.

Now the entire record structure is returned and not just the record that is changed.


\subsubsection{Cons}
During the development the idea occurred that every \verb\Record\ had to have its own type signature.
Because the field can be anything, it was thought to impact the type of the \verb\Record\.

This would be a problem when trying to declare the use of a specific \verb\Record\.

This resulted in the creation of \verb\Cons\.
\verb\Cons\ would be the type signature of that particular \verb\Record\.

It would consist of the types of the \verb\Label\, \verb\Field\ and \verb\Rest\.

\begin{lstlisting}
  TypeFunc "Cons" => Type
  Cons => (Label,Field,Rest)
\end{lstlisting}

It became apparent that every record is of type \verb\Record\ and that the type of \verb\Field\ had no impact on the type of the record.
\verb\Cons\ was then removed.

This also reduced the complexity of the records for the user.
Now the user can use the records without having to keep in mind which type belongs to which record.

